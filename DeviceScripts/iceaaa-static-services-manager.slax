/*
 * Filename      : iceaaa-static-services-manager.slax
 * Author        : cgiese cgiese@juniper.net
 * Version       : GIT_BUILD
 *
 * Description   : ICEAAA Static Service Manager GIT_BUILD
 *
 */

/* Junos Space specific context, name and description */
/* @CONTEXT = "/device" */
/* @NAME = "iceaaa-static-services-manager" */
/* @DESCRIPTION = "ICEAAA Static Service Manager GIT_BUILD" */
/* @ISLOCAL = "true" */
/* @EXECUTIONTYPE = "GROUPEDEXECUTION" */
/* @PROMOTE = "Yes" */
/* @PASSDEVICECREDENTIALS = "true" */

version 1.1;
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";

ns date = "http://exslt.org/dates-and-times";
ns str = "http://exslt.org/strings";
ns bit extension = "http://xml.libslax.org/bit";
ns func extension = "http://exslt.org/functions";
ns jspace = "http://jspace-utils/asharp@juniper.net";

import "../import/junos.xsl";

param $CONTEXT;

<xsl:key name="l2vpn" match="reference-site" use="local-site-id">;

match / {
    <op-script-results> {
        var $localtime = date:date-time();

        mvar $results = <info> "manager-results";

        /* parse credentials for selected devices */
        var $devices = jspace:credentials();
        if ( $devices ) {
            for-each( $devices/device) {
                var $local = jcs:open( (target) , (user) , (passwd) );
                var $devicename = (host);

                if ( $local//self::xnm:error ) {
                    set $connectionFailed = $connectionFailed +1;
                    append $results += <bng> {
                        <name> $devicename;
                        <connection-error>;
                    }
                } else {
                    /* get configuration ... */
                    var $getConfigurationMacros := {
                        <get-configuration> { 
                            <configuration> {
                                <groups> {
                                    <name> "iceaaa-dynamic";
                                }
                            }
                        }
                    }
                    var $macros = jcs:execute($local, $getConfigurationMacros);

                    var $getConfigurationStatic := {
                        <get-configuration> { 
                            <configuration> {
                                <groups> {
                                    <name> "static";
                                }
                            }
                        }
                    }
                    var $static = jcs:execute($local, $getConfigurationStatic);

                    var $getConfigurationDetails := {
                        <get-configuration> {
                            <configuration> {
                                <apply-macro> {
                                    <name> "DETAILS";
                                }
                            }
                        }
                    }
                    var $details = jcs:execute($local, $getConfigurationDetails);

                    var $getIfdDescriptions := {
                        <get-interface-information> {
                            <descriptions>;
                            <interface-name> "*e*/[0-9]";
                        }
                    }
                    var $ifdDescriptions = jcs:execute($local, $getIfdDescriptions);

                    var $getL2vpns := {
                        <get-l2vpn-connection-information> {
                            <up>;
                            <extensive>;
                        }
                    }
                    var $l2vpns = jcs:execute($local, $getL2vpns);

                    mvar $ifds = <empty>;

                    var $services := {
                        for-each($macros//apply-macro[ starts-with( name, "#ICEAAA#" ) ] ) {
                            var $macro = .;

                            /* get Line-Id and service VLAN (vid) from macro name */
                            var $serviceId = jcs:split( "[[.number-sign.]]", name );
                            var $lineId = $serviceId[3];
                            var $serviceVlan = $serviceId[4];
                            mvar $essm = false();

                            /* get service IFD and IFL */
                            mvar $ifd = "NA";
                            mvar $ifl = .//data[ name == "Service-Ifl" ]/value;
                            if ($ifl) {
                                set $ifd = jcs:split( "[[.period.]]", $ifl )[1];
                                set $essm = true();
                            } else {
                                var $ifdValue = $static//apply-macro[ contains( name, "#"_ $lineId _"#" ) ]/data[ name == "Service-Ifd" ]/value;
                                if($ifdValue) {
                                    set $ifd = $ifdValue;
                                }
                                set $ifl = $ifd _ "." _ $serviceVlan;
                            }
                            if(not( $ifds//ifd[ name == $ifd ] ) ) {
                                append $ifds += <ifd> {
                                    copy-of $ifdDescriptions//physical-interface[ name == $ifd ]/*;
                                }
                            }
                            <service> {
                                <ifd> $ifd;
                                if (not($essm)) {
                                    <static>;
                                }
                                <ifl> $ifl;
                                <macro> {
                                    copy-of $macro;
                                }
                                var $serviceString = $macro//data[ name == "Service-String" ]/value;
                                <service-string> $serviceString;
                                <line-id> $lineId;
                                <vid> $serviceVlan;
                                if(starts-with($serviceString, "ethp2p") ) {
                                    mvar $targetID = false();
                                    mvar $sourceID = false();
                                    mvar $siteId = false();
                                    mvar $rtId = false();
                                    if ($essm) {
                                        set $siteId = number($macro//data[ name == "Site-Id" ]/value);
                                        var $remoteId = number($macro//data[ name == "Remote-Site-Id" ]/value);
                                        set $rtId = number($macro//data[ name == "Route-Target-Id" ]/value);
                                        var $siteIdBin = bit:from-int($siteId, 14);
                                        var $remoteIdBin = bit:from-int($remoteId, 14);
                                        var $rtIdBin = bit:from-int($rtId, 16);
                                        set $targetID = format-number(bit:to-int($rtIdBin _ $remoteIdBin), "#");
                                        set $sourceID = format-number(bit:to-int($rtIdBin _ $siteIdBin), "#");
                                    } else {
                                        /* generate IDs from sourceID */
                                        set $targetID = number($macro//data[ name == "targetID" ]/value);
                                        set $sourceID = number($macro//data[ name == "sourceID" ]/value);
                                        var $sourceIdBin = bit:from-int($sourceID);
                                        var $sourceIdBinParts = jcs:regex("([0,1]{16})([0,1]*)", $sourceIdBin);
                                        set $siteId = bit:to-int( $sourceIdBinParts[3] );
                                        set $rtId = bit:to-int( $sourceIdBinParts[2] );
                                    }
                                    var $key = jcs:printf("ETHP2P-RT-%s-SITE-%s (%s)", $rtId, $siteId, $siteId);
                                    var $l2vpn := {
                                        for-each($l2vpns) {
                                            var $value = key( "l2vpn", $key );
                                            copy-of $value;
                                        }
                                    }
                                    var $connectionID = {
                                        if($sourceID < $targetID) {
                                            expr $sourceID;
                                        } else {
                                            expr $targetID;
                                        }
                                    }
                                    <ethp2p> {
                                        <site-id> $siteId;
                                        <instance> "ETHP2P-RT-" _ $rtId;
                                        <connection-id> $connectionID;
                                        <endpoint-id> $sourceID;
                                        if($l2vpn//local-site-id) {
                                            copy-of $l2vpn;
                                        }
                                    }
                                }
                            }
                        }
                    }

                    var $versionInfo = jcs:execute($local, "get-software-information");
                    append $results += <bng> {
                        <name> $devicename;
                        <info> {
                            <junos> $versionInfo//junos-version;
                            <plattform> $versionInfo//product-model;
                            <scripts> $details//data[ name = "SCRIPT-BUNDLE" ]/value;
                        }
                        for-each($ifds//ifd) {
                            var $ifd = .;
                            <ifd> {
                                copy-of $ifd/*;
                                mvar $serviceCounter = 0;
                                mvar $essm = true();
                                for-each($services//service[ ifd == $ifd/name ] ) {
                                    set $serviceCounter = $serviceCounter +1;
                                    var $service = .;
                                    copy-of $service;
                                    if ( .//static) {
                                        set $essm = false();
                                    }
                                }
                                <service-counter> $serviceCounter;
                                if ($essm) {
                                    <type> "ESSM";
                                } else {
                                    <type> "STATIC";
                                }
                            }
                        }
                    }
                }
            }
        }
        <output> {
            <HTML> {
                <HEAD> {
                    <title> "ICEAAA Static Services Manager - GIT_BUILD";
                    <style type="text/css"> {
                        expr "body { font-family: Verdana, Georgia, Arial, sans-serif;font-size: 12px;color:#000;border-style: solid;border-color: transparent;background-color: white}";
                        expr "td { font-family: Verdana, Georgia, Arial, sans-serif;font-size: 12px;color:#000; }";
                        expr "p { font-family: Verdana, Georgia, Arial, sans-serif;font-size: 14px;color:#000; }";
                        expr "table { font-size: 1.0em;border-collapse: collapse;width: 99% }";
                        expr "progress[value] { background-color: #eee; border-radius: 2px; box-shadow: 0 2px 5px rgba(0, 0, 0, 0.25) inset; }";
                        expr "#tableheader { font-weight: bolder;text-align: center;background-color: #7592A9;color: #fff }";
                        expr "#tableheaderleft { font-weight: bolder;text-align: left;background-color: #7592A9;color: #fff }";
                        expr "#tableheaderright { font-weight: bolder;text-align: right;background-color: #7592A9;color: #fff }";
                        expr "#tableheaderleft2 { font-weight: bolder;text-align: left;background-color: #A4A4A4;color: #fff }";
                        expr "#tablereverse { font-size: 16px; font-weight: bolder;text-align: left;background-color: #7592A9;color: #fff }";
                        expr "#celltitle { font-weight: bolder }";
                        expr "#cellwarning { text-align: center; background-color: #ff0000; color: #fff; font-weight: bolder; }";
                        expr "#cellamber { text-align: center; background-color: #f87431; color: #fff; font-weight: bolder; }";
                        expr "#center {text-align: center}";
                        expr "#failed { color: red }";
                        expr "#failed2 { color: red; font-style: italic; }";
                        expr "#content { margin-bottom: 15px; }";
                        expr ".footer { position: fixed; bottom: 0px; width: 100%; background-color: white; margin: 0px; }";
                        expr ".failed { color: red }";
                        expr ".failed2 { color: red; font-style: italic; }";
                        expr "picture.down {";
                        expr "  width:            16px;";
                        expr "  height:           16px;";
                        expr "  vertical-align:   top;";
                        expr "  display: inline-block;";
                        uexpr "  background-image: url('data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAAK3RFWHRDcmVhdGlvbiBUaW1lAE1vbiA0IE1heSAyMDA5IDE1OjA0OjQ3IC0wODAwYRO/pgAAAAd0SU1FB9kFBBYFKZkJTIsAAAAJcEhZcwAACxIAAAsSAdLdfvwAAAAEZ0FNQQAAsY8L/GEFAAACMElEQVR42o2Sz2sTQRTH3+xOfjQJJSDSJSRNUnqy9IfpwVsO4kGQHqpSSKAInrx6EURB/wL/AxERFRE86aknpSdR0pQkjYkNWU2mSEi7IUmTZpMZ32xSSWuiffB4s7vv+5nvvFkCQ/FqZuayFgo9ojYbQK8HXAirwqCa7TYwXX9wq1LZPNbQYQC2Bi4sLUVtxSKIVgvE4aGVHNdHEhAMwo98XhvWnADIEJ0OiFoNRLMJXAIG1eQcepp2uh2Uv95Iu7Kces1hdIwECNzNAh0nhjmo6n8BUiwHJ3MA6mJ2xjigr8PhD8Hl5aBs7pmml1LaFxKC26nAFQXaCHBgs93hgND8/JNNSh9L8c9CoUDR7kOn2/0pTKmH7++D2N0FISGmCbzRsAboQpicAU+nIaCqATE5CYamNUS3u67EdT2R2diI/QJ0OrAtpJAxILimLpeVdqcTHPjsRDdH09O9b5nMepyxhDWTd/V6LnpwYJyPRK7aGCN8bw/IxEQ/UUjsdiD4cxF01o5EYGt7+35M15+eGOpbw/gcbTbPabOzl9Rq9Q9AkQA8uzWPhQX4kkw+u5nP3xt5C92dnbuJbPY9LC7ixOx9iMcDxO0GZW4OtnK5jyYhd8Ze4xpeXouxWLJcTijhMH7Fz2hb9fshVSp9b5VKN9Yymc5YgAWpVBpGsbiSNYyS4vVaZ8/XakY5lVq5zlgVzhrPfb6LX1dX6+l4vPPS57tyZuFwvJiauvbG77/9r57fIIgQA1GLa40AAAAASUVORK5CYII=');";
                        expr "}";
                        expr "picture.up {";
                        expr "  width:            16px;";
                        expr "  height:           16px;";
                        expr "  vertical-align:   top;";
                        expr "  display: inline-block;";
                        uexpr "  background-image: url('data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAAK3RFWHRDcmVhdGlvbiBUaW1lAE1vbiA0IE1heSAyMDA5IDE1OjA3OjAwIC0wODAwy8k00QAAAAd0SU1FB9kFBBYHFh1ZAzQAAAAJcEhZcwAACxIAAAsSAdLdfvwAAAAEZ0FNQQAAsY8L/GEFAAACR0lEQVR42o1SS09TQRg9M3N721Ip0AcglCjIo2iiGEQlYlygggERF6xN/BHExMaGxJVxb9y5McZEF0ZcGBc+6kaN0bgwErFAqvKwLVJ723IfM05bJdAC+iWTeXzfOed7DMM2NhjyXWzus9dHI7nprWLYVo7+y56Thw/03fFU+847g8lHMy9zi/9NMBDyduzv6HlsOJIuqIa9UvUNN3aTu1PPU79KY2mZ8qUGb0tT56Tbb69J68tIaN/grrU1uWuqHwyHd1ZsSzAW3qsG6urut0pLaN9BpJsQgtjKJzTs8h5yOl23w+GNmA0X4jRu9HQdO7GYnpHAtVfkj9OJN2hp2z36Tmm6tmkPzk4Exvt7R8Z/ZKMw+SosbsASBkyhF84m1xHPxhAMdPdW7dMWPj9Lv10jOB2qHT119NzNDI1TnWfBiVFYIr+oCUPkCkRCWEgZS6StrmvAFfz5KhrJfmFy1gePdw0+3OFxOHSekUWZ4FSXuwGdaNCsJCzkQBmBqthBKQVnOmusah9xd2YnFcqUiUxOm0vPpsBh+fe0N9fGtPfIrCaQFnFQVUC1MXBLoLXyCOJz2SVm2jQuBBQV18n6hgxdqb9wZnDo1pT2VCrHQVjRLbiAZQoElG68jnwYe3I1ee8vRimda8ZaRgYJ2By0kHbe8uqECjBKSsM3I1gBk0BmozLFIsDUhWwgBxHlv5aWPwmgVIiU7FtlYBXmqqKCesDkT6K8iLBJUkuWoHDHP0oQ/Gv04/wLh92/UfFP6rOr82CCLayH/AaLhOXTdE+LDwAAAABJRU5ErkJggg==');";
                        expr "}";
                    }
                }
                <BODY> {
                    /* Java Script ... */
                    <script> {
                        expr "function toggleVisibility(id) {";
                        expr "    var buttonId=id.concat('button');";
                        expr "    if(document.getElementById(id).style.display == '') {";
                        expr "        document.getElementById(id).style.display = 'none';";
                        expr "        document.getElementById(buttonId).value = '+';";
                        expr "    } else {";
                        expr "        document.getElementById(id).style.display = '';";
                        expr "        document.getElementById(buttonId).value = '-';";
                        expr "    }";
                        expr "}";
                        expr "function openInNewWindow() {";
                        expr "    var myWindow = window.open('about:blank', 'ExportWindow', 'scrollbars=yes,menubar=yes,location=no');";
                        expr "    myWindow.document.write( document.getElementsByTagName( 'html' )[0].outerHTML );";
                        expr "    var openButton = myWindow.document.getElementById('openInNewWindowButton');";
                        expr "    openButton.parentNode.removeChild( openButton );";
                        expr "}";
                        expr "var lastFilter = false;";
                        expr "(function(document) {";
                        expr "    'use strict';";
                        expr "    var LightTableFilter = (function(Arr) {";
                        expr "        var _input;";
                        expr "        function _onInputEvent(e) {";
                        expr "            _input = e.target;";
                        expr "            var filter = _input.value.toLowerCase();";
                        expr "            if(filter.length === 0) {";
                        expr "                var allRows = document.getElementsByClassName('tr');";
                        uexpr "               for(var i = 0; i < allRows.length; i++) {";
                        expr "                    allRows[i].style.display = '';";
                        expr "                }";
                        expr "            }";
                        expr "            var togglerows = document.getElementsByClassName('toggle');";
                        uexpr "           for(var i = 0; i < togglerows.length; i++) {";
                        expr "                togglerows[i].style.display = 'none';";
                        expr "            }";
                        expr "            var togglebuttons = document.getElementsByClassName('toggle-button');";
                        uexpr "           for(var i = 0; i < togglebuttons.length; i++) {";
                        expr "                togglebuttons[i].value = '+';";
                        expr "            }";
                        uexpr "           if(filter.length > 0) {";
                        expr "                var rows = document.getElementsByClassName('search');";
                        expr "                lastFilter = filter;";
                        uexpr "               for(var i = 0; i < rows.length; i++) {";
                        expr "                    var element = rows[i];";
                        expr "                    var text = element.textContent.toLowerCase();";
                        uexpr "                   if(filter.length > 0 && text.indexOf(filter) !== -1) {";
                        expr "                        while(element.parentNode) {";
                        expr "                            element.style.display = '';";
                        expr "                            if(element.className.indexOf('toggle') !== -1){";
                        expr "                                var detailsButton = element.previousElementSibling.firstElementChild.firstElementChild;";
                        expr "                                if(detailsButton.tagName === 'INPUT') {";
                        expr "                                    detailsButton.value = '-';";
                        expr "                                }";
                        expr "                                if(element.className.indexOf('details') !== -1){";
                        expr "                                    element.previousElementSibling.style.display = '';";
                        expr "                                }";
                        expr "                            }";
                        expr "                            element = element.parentNode;";
                        expr "                        }";
                        expr "                    } else {";
                        expr "                        if(element.childNodes[1].tagName !== 'TH' ) {";
                        expr "                            element.style.display = 'none';";
                        expr "                            if(element.className.indexOf('details') !== -1){";
                        expr "                                element.previousElementSibling.style.display = 'none';";
                        expr "                            }";
                        expr "                        }";
                        expr "                    }";
                        expr "                }";
                        expr "           }";
                        expr "       }";
                        expr "       return {";
                        expr "           init: function() {";
                        expr "               var inputs = document.getElementsByClassName('light-table-filter');";
                        expr "               Arr.forEach.call(inputs, function(input) {";
                        expr "                   input.oninput = _onInputEvent;";
                        expr "               });";
                        expr "           }";
                        expr "       };";
                        expr "    })(Array.prototype);";
                        expr "    document.addEventListener('readystatechange', function() {";
                        expr "        if (document.readyState === 'complete') {";
                        expr "            LightTableFilter.init();";
                        expr "        }";
                        expr "    });";
                        expr "})(document);";
                        expr "function macroVisibility(cb) {";
                        expr "    var macros = document.getElementsByClassName('macro');";
                        uexpr "   for(var i = 0; i < macros.length; i++) {";
                        expr "        if(cb.checked) {";
                        expr "            macros[i].style.display = '';";
                        expr "        } else {";
                        expr "            macros[i].style.display = 'none';";
                        expr "        }";
                        expr "    }";
                        expr "}";
                    }
                    <div id="content"> {
                        <p style="text-align: center; font-family: Verdana, Georgia, Arial, sans-serif, bold; font-size: 18px; color:#000;"> { 
                            expr "ICEAAA Static Services Manager - GIT_BUILD";
                        }
                    }
                    <input id="openInNewWindowButton" type="Button" value="Open in Window" onclick="openInNewWindow(); return false;">;

                    <input type="search" class="light-table-filter" data-table="order-table" placeholder="Filter">;
                    <label> {
                        expr "Display Macros";
                        <input type="checkbox" onclick="macroVisibility(this);">;
                    }


                    for-each ( $results//bng) {
                        <p> {
                            expr jcs:printf("%s Plattform: %s Junos: %s Scripts: %s", name, info/plattform, info/junos, info/scripts);
                        }
                        <p> {
                        	expr "DEBUG";
                            copy-of .;
                        }
                        <table class="bng-table table"> {
                            <colgroup> {
                                <col style="width:50px; white-space:nowrap;">;
                                <col style="width:100px; white-space:nowrap;">;
                                <col style="width:80px; white-space:nowrap;">;
                                <col style="width:200px; white-space:nowrap;">;
                                <col style="white-space:nowrap;">;
                                <col style="width:150px; white-space:nowrap;">;
                                <col style="width:50px; white-space:nowrap;">;
                            }
                            <tr class="tr"> {
                                <th id="tableheaderleft"> {
                                    expr "";
                                }
                                <th id="tableheaderleft"> {
                                    expr "INTERFACE";
                                }
                                <th id="tableheaderleft"> {
                                    expr "TYPE";
                                }
                                <th id="tableheaderleft"> {
                                    expr "LINE-ID";
                                }
                                <th id="tableheaderleft"> {
                                    expr "DESCRIPTION";
                                }
                                <th id="tableheaderleft"> {
                                    expr "STATUS";
                                }
                                <th id="tableheaderleft"> {
                                    expr "SERVICES";
                                }
                            }
                            for-each ( ./ifd ) {
                                var $interface = name;
                                mvar $essm = true();
                                if( type == "STATIC" ) {
                                    set $essm = false();
                                }
                                var $detailsTag = "details-" _ name;
                                var $detailsTagButton = $detailsTag _ "button";

                                <tr class="tr"> {
                                    <td> {
                                        <input class="toggle-button input" style="width: 22px" id=$detailsTagButton type="Button" value="+" onclick="toggleVisibility('" _ $detailsTag _ "');">;
                                    }
                                    <td> {
                                        expr $interface;
                                    }
                                    <td> {
                                        expr type;
                                    }
                                    <td> {
                                        if($essm) {
                                           expr "";
                                        } else {
                                            expr .//line-id;
                                        }
                                    }
                                    <td> {
                                        expr description;
                                    }
                                    <td> {
                                        expr "ADMIN: ";
                                        if( admin-status == "up" ) {
                                            <picture class="up"> {
                                                expr "";
                                            }
                                        } else {
                                            <picture class="down"> {
                                                expr "";
                                            }
                                        }
                                        expr " OPER: ";
                                        if( oper-status == "up" ) {
                                            <picture class="up"> {
                                                expr "";
                                            }
                                        } else {
                                            <picture class="down"> {
                                                expr "";
                                            }
                                        }
                                    }
                                    <td> {
                                        expr service-counter;
                                    }
                                }
                                <tr class="toggle tr" id=$detailsTag style="display: none;"> {
                                    <td> {
                                        expr "";
                                    }
                                    <td colspan="6"> {
                                       <table class="order-table table"> {
                                            <colgroup> {
                                                <col style="width:50px; white-space:nowrap;">;
                                                if($essm) {
                                                    <col style="width:200px; white-space:nowrap;">;
                                                }
                                                <col style="width:80px; white-space:nowrap;">;
                                                <col style="width:120px; white-space:nowrap;">;
                                                <col style="white-space:nowrap;">;
                                            }
                                            <tr class="tr"> {
                                                <th id="tableheaderleft2"> {
                                                    expr "";
                                                }
                                                if($essm) {
                                                    <th id="tableheaderleft2"> {
                                                        expr "Line-Id";
                                                    }
                                                }
                                                <th id="tableheaderleft2"> {
                                                    expr "VID";
                                                }
                                                <th id="tableheaderleft2"> {
                                                    expr "TYPE";
                                                }
                                                <th id="tableheaderleft2"> {
                                                    expr "SUMMARY";
                                                }
                                            }
                                            for-each (.//service) {
                                                var $serviceName = substring-before( service-string, "(" );
                                                mvar $detailsTagService = "details-" _ $interface _ "-" _ vid;
                                                mvar $summaryTagService = "summary-" _ $interface _ "-" _ vid;
                                                if($essm) {
                                                    set $detailsTagService = "details-" _ line-id _ "-" _ vid;
                                                    set $summaryTagService = "summary-" _ line-id _ "-" _ vid;
                                                }
                                                var $detailsTagServiceButton = $detailsTagService _ "button";
                                                mvar $ethp2p = false();
                                                mvar $ethp2pSiteId = false();
                                                mvar $ethp2pInstance = false();
                                                mvar $ethp2pFlag = false();
                                                mvar $ethp2pSite = false();
                                                mvar $ethp2pConnection = false();
                                                if(.//ethp2p) {
                                                    set $ethp2p = true();
                                                    set $ethp2pSiteId = .//ethp2p/site-id;
                                                    set $ethp2pInstance = .//ethp2p/instance;
                                                    set $ethp2pFlag = .//ethp2p//interface-flags-description;
                                                    set $ethp2pSite = $ethp2pInstance _ "-SITE-" _ $ethp2pSiteId;
                                                    set $ethp2pConnection = .//ethp2p/connection-id;
                                                }
                                                <tr class="search tr" id=$summaryTagService> {
                                                    <td> {
                                                        <input class="toggle-button input" style="width: 22px" id=$detailsTagServiceButton type="Button" value="+" onclick="toggleVisibility('" _ $detailsTagService _ "');">;
                                                    }
                                                    if($essm) {
                                                        <td> {
                                                            expr line-id;
                                                        }
                                                    }
                                                    <td> {
                                                        expr vid;
                                                    }
                                                    <td> {
                                                        expr $serviceName;
                                                    }
                                                    <td> {
                                                        if($ethp2p) {
                                                            var $ethp2pFlagSummary = {
                                                               if(string-length( $ethp2pFlag ) > 0 ) {
                                                                   expr " Flag: " _ $ethp2pFlag;
                                                               } else {
                                                                   expr "";
                                                               }
                                                            }
                                                            expr jcs:printf("Connection-ID: %s Site: %s%s", $ethp2pConnection, $ethp2pSite, $ethp2pFlagSummary);
                                                        }
                                                    }
                                                }
                                                <tr class="search toggle details tr" id=$detailsTagService style="display: none;"> {
                                                    <td> {
                                                        expr "";
                                                    }
                                                    <td colspan="5"> {
                                                        <table> {
                                                            <colgroup> {
                                                                <col style="width:200px; white-space:nowrap;">;
                                                                <col style="white-space:nowrap;">;
                                                            }
                                                            <tr class="tr"> {
                                                                <td> {
                                                                    expr "Service-String";
                                                                }
                                                                <td> {
                                                                    expr .//service-string;
                                                                }
                                                            }
                                                            <tr class="tr"> {
                                                                <td> {
                                                                    expr "Service-Interface";
                                                                }
                                                                <td> {
                                                                    expr .//ifl;
                                                                }
                                                            }
                                                            if($ethp2p) {
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Connection-Id";
                                                                    }
                                                                    <td> {
                                                                        expr $ethp2pConnection;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Endpoint-Id";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p/endpoint-id;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Site-Id";
                                                                    }
                                                                    <td> {
                                                                        expr $ethp2pSiteId;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Instance";
                                                                    }
                                                                    <td> {
                                                                        expr $ethp2pInstance;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Flag";
                                                                    }
                                                                    <td> {
                                                                        expr $ethp2pFlag;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Local Interfaces UP";
                                                                    }
                                                                    var $ethp2pIfUp = .//ethp2p//num-local-interfaces-up;
                                                                    var $ethp2pIf = .//ethp2p//num-local-interfaces;
                                                                    if ( $ethp2pIfUp < $ethp2pIf ) {
                                                                        <td class="failed"> {
                                                                            expr .//ethp2p//num-local-interfaces-up _ "/" _ .//ethp2p//num-local-interfaces;
                                                                        }
                                                                    } else {
                                                                        <td> {
                                                                            expr .//ethp2p//num-local-interfaces-up _ "/" _ .//ethp2p//num-local-interfaces;
                                                                        }
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Base";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-base;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Offset";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-offset;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Size";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-size;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Range";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-range;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Preference";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-preference;
                                                                    }
                                                                }
                                                                <tr class="tr"> {
                                                                    <td> {
                                                                        expr "Label Status Vector";
                                                                    }
                                                                    <td> {
                                                                        expr .//ethp2p//label-block-status-vector;
                                                                    }
                                                                }
                                                            }
                                                            for-each( .//macro//data ) {
                                                                <tr class="macro" style="display: none"> {
                                                                    <td> {
                                                                        expr "Macro " _ name;
                                                                    }
                                                                    <td> {
                                                                        expr value;
                                                                    }
                                                                }
                                                            }
                                                        }
                                                    }
                                                }
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }
                    var $duration = date:seconds() - date:seconds( $localtime );
                    <p class="footer" style="text-align: left; font-family: Verdana, Georgia, Arial, sans-serif; font-size: 9px; color:#000;"> { 
                        expr "Date: " _ $localtime _ " Execution Time: " _ $duration _ "s";
                    }
                } /* ... BODY */
            }
        }
    }
}
/*
 * This function reads the credentials that are automatically supplied to the script 
 * when the annotation to pass the device credentials is used. this is a requirement
 * for a locally executed grouped execution script, as the script must handle all
 * device connections.
 */
<func:function name="jspace:credentials"> {
    if( $CONTEXT ) {
        var $splitCredentials = str:split( $credentials, "\\;" );
        var $targets := {
            for-each ( $splitCredentials ) {
                var $splitCredential = str:split( ., "\\:" );
                var $user-target = str:split($splitCredential[1], "\@");
                var $host = substring-before( substring-after( $deviceipmap, substring-after( $splitCredential[1], "@" ) _ "\":\"" ), "\"" );
                <device> {
                    <target> {
                        expr $user-target[2];
                    }
                    <user> {
                        expr $user-target[1];
                    }
                    <passwd> {
                        expr $splitCredential[2];
                    }
                    <host> {
                        expr $host;
                    }
                }
            }
        }
        <func:result select="$targets">;
    }
    else {
        <func:result select="false()">;
    }
}