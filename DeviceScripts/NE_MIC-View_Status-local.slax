/*
 * Filename      : NE_MIC-View_Status-local.slax
 * Author        : Andrew Sharp asharp@juniper.net
 * Platform      : Junos Space
 * Release       : 13.1P2
 * Version       : 1.0
 * SVN INFO      :
 *
 * $Rev: 36133 $
 * $Date: 2014-04-24 14:14:09 +0100 (Thu, 24 Apr 2014) $
 * $Author: asharp $
 * 
 * Description   : Show Physical Interface Cards that are part of Modular Interface Cards within an FPC.
 *
 */

version 1.0;

/* Namespace declarations */
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns jspace = "http://jspace-utils/asharp@juniper.net";
ns exsl extension = "http://exslt.org/common";

/* Imports */
import "../import/junos.xsl";
import "lc-jspace-lib.slax";

/* Junos Space specific context, name and description */
/* @CONTEXT = "/device/chassis-inventory/chassis/chassis-module[starts-with(name,"FPC")]/chassis-sub-module[starts-with(name,"MIC")]" */
/* @NAME = "View MIC Status" */
/* @DESCRIPTION = "Show Physical Interface Cards that are part of Modular Interface Cards within an FPC." */
/* @ISLOCAL = "true" */

/* Global variables */
var $scriptname = "NE_MIC-View_Status-local.slax";
var $get-pic-information-rpc;
var $results;
var $local = jcs:open();

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "The context associated with this script.";
	}
}
param $CONTEXT;

match / {
	<op-script-results> {
		/* abort if no connection to local mgd */
		if ($local/..//xnm:error) {
			call rpc_failure($rpc = $local/.., $message = "Error connecting on mgd on this RE");
			<xsl:message terminate="yes"> ;
		}
		/* Request status for a single FPC */
		var  $get-pic-information-rpc = <get-pic-information> {
			<fpc-slot> jspace:parse-context-chassis("/device.*/chassis-inventory/chassis.*/chassis-module\\[name=\".* ([0-9]+)\"\\]/chassis-sub-module.*",$CONTEXT);
		}
		var $results = jcs:execute( $local , $get-pic-information-rpc );
		if ($results/..//xnm:error) {
			call rpc_failure($rpc = $results/.., $message = "Error collecting pic information.");
			<xsl:message terminate="yes"> ;
		}

		<output> {
			<HTML> {
				<HEAD> {
					<title> "Physical Interface Card status";
					copy-of jspace:html-style("3");
				}
				<BODY> {
					<table border="1"> {
						<tr> {
							<td id="tableheader"> {
								expr "Component";
							}
							<td id="tableheader"> {
								expr "Status";
							}
							<td id="tableheader"> {
								expr "Description";
							}
						}
						for-each ($results/fpc) {
							<td> {
								expr "Slot " _ (slot);
							}
							<td> {
								expr (state);
							}
							<td> {
								expr (description);
							}
							for-each ( $results/fpc/pic ) {
								<tr> {
									<td> {
										expr "PIC " _ (pic-slot);
									}
									<td> {
										expr (pic-state);
									}
									<td> {
										expr (pic-type);
									}
								}
							}
						}
					}
				}
			}
		}
		expr jcs:syslog("daemon.info", "SCRIPT_ACTION_MIC_STATUS: Script action taken to show status of the Modular Interface Card (MIC).");
		var $close-results = jcs:close( $local );
		if ($close-results/..//xnm:error) {
			call rpc_failure($rpc = $close-results/.., $message = "Error closing connection.");
			<xsl:message terminate="yes"> ;
		}
	}
}

template rpc_failure($rpc, $message = "Following errors occurred while trying to gather data: ") {
  expr jcs:syslog("daemon.error", $message);
  for-each ($rpc//xnm:error) {
    expr jcs:syslog("daemon.error", message);
  }
}
