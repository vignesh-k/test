/* 
	Script to show the IS-IS database (show isis database).
*/

/*
	Author: bcersows
	Version: 1.0
	Date: 14.04.2014
	Last Modified By: bcersows
	###Script_showISISDatabase_Betrieb.slax;v1.0;2014.04.14
*/

version 1.1;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
/*ns slax = "http://xml.libslax.org/slax";*/
ns exsl extension = "http://exslt.org/common";
ns curl extension = "http://xml.libslax.org/curl";
ns date = "http://exslt.org/dates-and-times";

ns bcersows = "http://bcersows@telekom.de";
import "Space_Library-Local.slax";

import "../import/junos.xsl";

/* @CONTEXT = "/device" */
/* @NAME = "Looking Glass IS-IS Database Information" */
/* @DESCRIPTION = "Shows IS-IS information." */
/* @ISLOCAL = "true" */
/* @EXECUTIONTYPE = "SINGLEEXECUTION" */
/* @VARIABLECONTEXT = "[{'name':'DEVICE','defaultvaluexpath':'/device/name/text()'}]" */

mvar $counter = 0;

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "Context beim Aufruf.";
	}
}
param $CONTEXT;

match / {
<op-script-results> {
/*template junoscript() {*/
	mvar $results;
	mvar $configRpcs;
	mvar $unlock-results;
	
	mvar $localtime = date:date-time();
	
	var $myDevice = {
		if ( false() ) {
			expr $DEVICE;
		} else {
			/*if ( jcs:regex( "/device\\[name=\".*\"\\]/configuration/access/radius-server\\[name=\".*\"\\]", $CONTEXT ) ) {
				expr jcs:regex( "/device\\[name=\"(.*)\"\\]/configuration/access/radius-server\\[name=\"(.*)\"\\]", $CONTEXT )[2];
			} else {
				expr jcs:regex( "/device\\[name=\"(.*)\"\\].*", $CONTEXT )[2];
			}*/
			expr jcs:regex( "/device\\[name=\"([^\"]*)\"\\].*", $CONTEXT )[2];
		}
	}
	
	/*
	*	Get configuration.
	*/
	
	var $connection = jcs:open();
	
	<output> {
		<HTML> {
			<HEAD> {
				<title> "Informationen ueber BNG " _ $myDevice _ ".";
				copy-of bcersows:insertCSS();
				copy-of bcersows:insertJS();
			}
			<BODY> {

				copy-of bcersows:insertHeaderBig( $myDevice );
				
				copy-of bcersows:insertCompletionTest();
				
				if ( $connection ) { } else {	
					<div class="error"> {
						expr "Konnte keine Verbindung aufbauen."; <br>;
						expr "Abbruch folgt.";
					}
				}	
				
				/*<div id="floatingButton" onclick="toggleAll(this);"> { 
					expr "Alle oeffnen.";
				}*/
				
				copy-of bcersows:insertSoftwareInformation( $connection );
				
				var $rpc = "get-isis-database-information";
				set $results = jcs:execute( $connection, $rpc );
				call checkErrors( $resultsNodeSet = $results, $connection, $msg = "Fehler." );
				
				<div> {
					<p class="bold subheader">{
						<span class="left"> { expr "IS-IS Database Level 1"; }
						copy-of bcersows:insertDownloadButton( "resultsTable1", "isisDatabaseL1" );
						if ( !$results/isis-database[level="1"]/isis-database-entry ) {
							copy-of bcersows:disableDownloadButton();
						}
					}
					<p> {
						expr "There are " _ $results/isis-database[level="1"]/lsp-count _ " entries." ;
					}
					if ( $results/isis-database[level="1"]/isis-database-entry ) {
						<table id="resultsTable1" class="xwide" border="1"> {
							<tr> {
								<th> { expr "LSP ID"; }
								<th> { expr "Sequence-Number"; }
								<th> { expr "Checksum"; }
								<th> { expr "Lifetime"; }
								<th> { expr "Attributes"; }
							}
							for-each ( $results/isis-database[level="1"]/isis-database-entry ) {
								<tr> {
									<td> { expr ./lsp-id; }
				                    <td> { expr ./sequence-number; }
	                                <td> { expr ./checksum;}
									<td> { expr ./remaining-lifetime; }
									<td> { expr ./lsp-attributes; }
								}
							}
							/*expr $results;*/
						}
					} 
					
					copy-of bcersows:insertBackButton();
				}
				
				<div> {
					<p class="bold subheader">{
						<span class="left"> { expr "IS-IS Database Level 2"; }
						copy-of bcersows:insertDownloadButton( "resultsTable2", "isisDatabaseL2" );
						if ( !$results/isis-database[level="2"]/isis-database-entry ) {
							copy-of bcersows:disableDownloadButton();
						}
					}
					<p> {
						expr "There are " _ $results/isis-database[level="2"]/lsp-count _ " entries." ;
					}
					if ( $results/isis-database[level="2"]/isis-database-entry ) {
						<table id="resultsTable2" class="xwide" border="1"> {
							<tr> {
								<th> { expr "LSP ID"; }
								<th> { expr "Sequence-Number"; }
								<th> { expr "Checksum"; }
								<th> { expr "Lifetime"; }
								<th> { expr "Attributes"; }
							}
							for-each ( $results/isis-database[level="2"]/isis-database-entry ) {
								<tr> {
									<td> { expr ./lsp-id; }
				                    <td> { expr ./sequence-number; }
	                                <td> { expr ./checksum;}
									<td> { expr ./remaining-lifetime; }
									<td> { expr ./lsp-attributes; }
								}
							}
							/*expr $results;*/
						}
					} 
					
					copy-of bcersows:insertBackButton();
				}
				
				var $duration = date:seconds() - date:seconds( $localtime );
				<div class="wholeLine"> {
					<span class="left italic unselectable"> "vs. 1.0";
					<span class="right"> { 
						expr "Dauer der Ausf"; <xsl:text disable-output-escaping="yes"> "&uuml;"; expr "hrung: " _ $duration _ "s. ";
					}
				}
				
				/*set $configRpcs = <clear-aaa-statistics-table> { <accounting>; }
				set $results = jcs:execute( $connection, $configRpcs );
				call checkErrors( $resultsNodeSet = $results, $connection, $msg = "Fehler." );*/
			}
		}
	}

	set $unlock-results = jcs:execute( $connection, "unlock-database" );
	expr jcs:close( $connection );
	
}
}

template checkErrors ( $resultsNodeSet, $msg = "", $connection ) {
	if( $resultsNodeSet//self::xnm:error ) {
		copy-of bcersows:outputErrorDiv($resultsNodeSet, $msg);
	}
}
