/*
 * Filename      : NE_FPC-Set_Online-local.slax
 * Author        : Andrew Sharp asharp@juniper.net
 * Platform      : Junos Space
 * Release       : 13.1P2
 * Version       : 1.0
 * SVN INFO      :
 *
 * $Rev: 36133 $
 * $Date: 2014-04-24 14:14:09 +0100 (Thu, 24 Apr 2014) $
 * $Author: asharp $
 * 
 * Description   : Take FPC online.
 *
 */

version 1.0;

/* Namespace declarations */
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns jspace = "http://jspace-utils/asharp@juniper.net";

/* Imports */
import "../import/junos.xsl";
import "lc-jspace-lib.slax";

/* Junos Space specific context, name and description */
/* @CONTEXT = "/device/chassis-inventory/chassis/chassis-module[starts-with(name,"FPC")]" */
/* @NAME = "Bring FPC Online" */
/* @DESCRIPTION = "Take FPC online." */
/* @ISLOCAL = "true" */

/* Global variables */
var $scriptname = "NE_FPC-Set_Online-local.slax";
var $results;
var $regex;
var $result-regex;
var $local = jcs:open();

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "The context associated with this script.";
	}
}
param $CONTEXT;

match / {
	<op-script-results> {
		/* abort if no connection to local mgd */
		if ($local/..//xnm:error) {
			call rpc_failure($rpc = $local/.., $message = "Error connecting on mgd on this RE");
			<xsl:message terminate="yes"> ;
		}
		var $regex = "/device.*/chassis-inventory/chassis\\[name=\"(.*)\"\\]/chassis-module\\[name=\"(.* ([0-9]+))\"\\]";
		var $result-regex = jcs:regex( $regex , $CONTEXT );
                
		/* Request FPC online */
		var $command = {
			<command> "request chassis fpc online slot " _ $result-regex[4];
		}
		var $results = jcs:execute( $local , $command);
		if ($results/..//xnm:error) {
			call rpc_failure($rpc = $results/.., $message = "Error while executing command request chassis fpc online slot");
			<xsl:message terminate="yes"> ;
		}

		<output> {
			<HTML> {
				<HEAD> {
					<title> "FPC online";
					copy-of jspace:html-style("1");
				}
				<BODY> {
					<p> {
						copy-of $results;
					}
				}
			}
		}
		if ( contains( $results , "already online" ) ) {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_FPC_ONLINE: Script action failed to online the Flexible PIC Concentrator (FPC) as it is online already.");
		}
		else {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_FPC_ONLINE: Script action taken to online the Flexible PIC Concentrator (FPC).");
		}
		var $close-results = jcs:close( $local );
		if ($close-results/..//xnm:error) {
			call rpc_failure($rpc = $close-results/.., $message = "Error closing connection.");
			<xsl:message terminate="yes"> ;
		}
	}
}

template rpc_failure($rpc, $message = "Following errors occurred while trying to gather data: ") {
  expr jcs:syslog("daemon.error", $message);
  for-each ($rpc//xnm:error) {
    expr jcs:syslog("daemon.error", message);
  }
}
