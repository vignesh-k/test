/*
 * Filename      : NE_CB-Set_Online-local.slax
 * Author        : Andrew Sharp asharp@juniper.net
 * Platform      : Junos Space
 * Release       : 13.1P2
 * Version       : 1.0
 * SVN INFO      :
 *
 * $Rev: 36133 $
 * $Date: 2014-04-24 14:14:09 +0100 (Thu, 24 Apr 2014) $
 * $Author: asharp $
 * 
 * Description   : Take CB online.
 *
 */
 
version 1.0;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns jspace = "http://jspace-utils/asharp@juniper.net";

import "../import/junos.xsl";
import "lc-jspace-lib.slax";

/* @CONTEXT = "/device/chassis-inventory/chassis/chassis-module[starts-with(name,"CB")]" */
/* @NAME = "Bring Control Board Online" */
/* @DESCRIPTION = "Take CB online." */
/* @ISLOCAL = "true" */

/*
 * Global variables
*/
var $results;
var $local = jcs:open();

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "The context associated with this script.";
	}
}
param $CONTEXT;

match / {
	<op-script-results> {
		/* abort if no connection to local mgd */
		if ($local/..//xnm:error) {
			call rpc_failure($rpc = $local/.., $message = "Error connecting on mgd on this RE");
			<xsl:message terminate="yes"> ;
		}
		var $command = {
			<command> "request chassis cb online slot " _ jspace:parse-context-chassis("/device.*/chassis-inventory/chassis.*/chassis-module\\[name=\".* ([0-9]+)\"\\]",$CONTEXT);
		}
		
		var $results = jcs:execute( $local , $command );
		if ($results/..//xnm:error) {
			call rpc_failure($rpc = $results/.., $message = "Error bringing cb online.");
			<xsl:message terminate="yes"> ;
		}

		<output> {
			<HTML> {
				<HEAD> {
					<title> "CB online";
					copy-of jspace:html-style("1");
				}
				<BODY> {
					<p> {
						copy-of $results;
					}
				}
			}
		}
		if ( contains( $results , "online already" ) ) {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_CB_ONLINE: Script action failed to online the Control Board (CB) as it is online already.");
		}
		else {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_CB_ONLINE: Script action taken to online the Control Board (CB).");
		}

		var $close-results = jcs:close( $local );
		if ($close-results/..//xnm:error) {
			call rpc_failure($rpc = $close-results/.., $message = "Error closing connection");
			<xsl:message terminate="yes"> ;
		}
		
	}
}

template rpc_failure($rpc, $message = "Following errors occurred while trying to gather data: ") {
  expr jcs:syslog("daemon.error", $message);
  for-each ($rpc//xnm:error) {
    expr jcs:syslog("daemon.error", message);
  }
}
