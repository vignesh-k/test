/* 	
	Betrieb: Script to request some information from the BNG and send it to a remote server. Manual version of the event Script.
*/

/*
	Author: bcersows
	Version: 1.1
	Date: 14.08.2014
	Last Modified By: bcersows
	###Script_NE Support - Collect BNG Juniper Inventar.slax;v1.1;2014.08.14
*/

/*
	Changelog:
	----------
	1.1; 14.08.2014: 
		- Set default parameters to the ones of NSO.
		- Added error messages to copy result output.
*/

version 1.1;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
/*ns slax = "http://xml.libslax.org/slax";*/
ns date = "http://exslt.org/dates-and-times";
ns str = "http://exslt.org/strings";

ns bcersows = "http://bcersows@telekom.de";
import "Space_Library-Local.slax";

import "../import/junos.xsl";

/* @CONTEXT = "/device" */
/* @NAME = "NE Support - Collect BNG Juniper Inventar" */
/* @DESCRIPTION = "Script to request some information from the BNG and send it to a remote server. Manual version of the event Script." */
/* @ISLOCAL = "true" */
/* @EXECUTIONTYPE = "SINGLEEXECUTION" */
/* @VARIABLECONTEXT = "[{'name':'Server','defaultvalue':'153.17.33.219'},{'name':'outputPath','defaultvalue':'/zfs/ftp/acc-jun/incoming'},{'name':'username','defaultvalue':'bicobng'}]" */
/* @PROMOTE="yes" */

mvar $counter = 0;
mvar $output = "";
mvar $contentToWrite = "";
var $version = "1.1";

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "Context beim Aufruf.";
	}
	<argument> {
		<name> "Server";
		<description> "Remote server. Standard servers are 153.17.33.219 and 153.17.33.220.";
	}
	<argument> {
		<name> "outputPath";
		<description> "Path of output file. File name will be '[BNG Name]_[type]_[YYYYmmDD]_bng_inventory'";
	}
	<argument> {
		<name> "username";
		<description> "Username to use on remote server.";
	}
}
param $CONTEXT;
param $Server;
param $outputPath;
param $username;

match / {
	<op-script-results> {
	
		mvar $localtime = date:date-time();
	
		var $myDevice = {
			if ( false() ) {
				expr $DEVICE;
			} else {
				expr jcs:regex( "/device\\[name=\"([^\"]*)\"\\].*", $CONTEXT )[2];
			}
		}
		
		var $connection = jcs:open();
		if ($connection/..//xnm:error) {
			<error> "Error connecting on mgd on this RE";
			<xsl:message terminate="yes"> ;
		}
		
		<output> {
			<HTML> {
				<HEAD> {
					<title> "Informationen ueber BNG " _ $myDevice _ ".";
					copy-of bcersows:insertCSS();
					copy-of bcersows:insertJS();
				}
				<BODY> {
					copy-of bcersows:insertHeaderBig( $myDevice );
					copy-of bcersows:insertCompletionTest();
					
					if ( $connection ) { } else {	
						<div class="error"> {
							expr "Konnte keine Verbindung aufbauen."; <br>;
							expr "Abbruch folgt.";
						}
					}	
					
					copy-of bcersows:insertSoftwareInformation( $connection );
					
					var $outputFileTime = date:year() _ "" _ format-number( date:month-in-year(), "00" ) _ "" _ format-number( date:day-in-month(), "00" );	
					var $destinationPath = {
						if ( $Server != "" ) {
							expr "scp://";
							if ( $username != "" ) {
								expr $username;
								expr "@";
							}
							expr $Server _ "";
						}
						expr $outputPath;
						if ( substring( $outputPath , string-length( $outputPath ) , 1 ) != "/" ) { 
							expr "/"; 
						}
					}
					
					var $hardwareRpc = <get-chassis-inventory> {
						<detail>;
					}
					var $hardwareResults = jcs:execute( $connection, $hardwareRpc );
					call checkErrors( $resultsNodeSet = $hardwareResults, $connection, $msg = "Fehler." );
					
					var $versionResults = jcs:execute( $connection, "get-software-information" );
					call checkErrors( $resultsNodeSet = $versionResults, $connection, $msg = "Fehler." );
					
					var $plaintextHardware = {
						expr "Item, Version, Part Number, Serial Number, Description\r\n\r\n";
						for-each ( $hardwareResults/chassis/chassis-module ) {
							expr ./name _ "," _ ./version _ "," _ ./part-number _ "," _ ./serial-number _ "," _ ./model-number _ "\r\n";
							/* if RE, loop through disk modules. */
							for-each ( current()/chassis-re-disk-module ) {
								expr "\t" _ ./name _ "," _ ./disk-size _ "," _ ./model _ "," _ ./serial-number _ "," _ ./description _ "\r\n";
							}
							for-each ( current()/chassis-re-usb-module ) {
								expr "\t" _ ./name _ "," _ ./product _ "," _ ./product-number _ "," _ ./vendor _ "," _ ./description _ "\r\n";
							}
							/* if FPC, loop through sub and sub sub modules. */
							for-each ( current()/chassis-sub-module ) {
								expr "\t" _ ./name _ "," _ ./version _ "," _ ./part-number _ "," _ ./serial-number _ "," _ ./model-number _ "\r\n";
								for-each ( current()/chassis-sub-sub-module ) {
									expr "\t\t" _ ./name _ "," _ ./part-number _ "," _ ./serial-number _ "," _ ./description _ "\r\n";
									for-each ( current()/chassis-sub-sub-sub-module ) {
										expr "\t\t\t" _ ./name _ "," _ ./part-number _ "," _ ./serial-number _ "," _ ./description _ "\r\n";
									}
								}
							}
						}
					}
					var $xmlHardware = {
						expr "<chassis-inventory  xmlns=\"http://xml.juniper.net/junos/13.1X49/junos-chassis\">";
							expr "<name>" _ $hardwareResults/chassis/name _ "</name>\r\n";
							expr "<serial-number>" _ $hardwareResults/chassis/serial-number _ "</serial-number>\r\n";
							expr "<description>" _ $hardwareResults/chassis/description _ "</description>\r\n";
							for-each ( $hardwareResults/chassis/chassis-module ) {
								expr "<chassis-module>\r\n";
									expr "<name>" _ ./name _ "</name>\r\n";
									expr "<version>" _ ./version _ "</version>\r\n";
									expr "<part-number>" _ ./part-number _ "</part-number>\r\n";
									expr "<serial-number>" _ ./serial-number _ "</serial-number>\r\n";
									expr "<description>" _ ./description _ "</description>\r\n";
									expr "<clei-code>" _ ./clei-code _ "</clei-code>\r\n";
									expr "<model-number>" _ ./model-number _ "</model-number>\r\n";
									/* if RE, loop through disk modules. */
									for-each ( current()/chassis-re-disk-module ) {
										expr "<chassis-re-disk-module>\r\n";
											expr "<name>" _ ./name _ "</name>\r\n";
											expr "<disk-size>" _ ./disk-size _ "</disk-size>\r\n";
											expr "<model>" _ ./model _ "</model>\r\n";
											expr "<serial-number>" _ ./serial-number _ "</serial-number>\r\n";
											expr "<description>" _ ./description _ "</description>\r\n";
										expr "</chassis-re-disk-module>\r\n";
									}
									for-each ( current()/chassis-re-usb-module ) {
										expr "<chassis-re-usb-module>\r\n";
											expr "<name>" _ ./name _ "</name>\r\n";
											expr "<product>" _ ./product _ "</product>\r\n";
											expr "<product-number>" _ ./product-number _ "</product-number>\r\n";
											expr "<vendor>" _ ./vendor _ "</vendor>\r\n";
											expr "<description>" _ ./description _ "</description>\r\n";
										expr "</chassis-re-usb-module>\r\n";
									}
									/* if FPC, loop through sub and sub sub modules. */
									for-each ( current()/chassis-sub-module ) {
										expr "<chassis-sub-module>\r\n";
											expr "<name>" _ ./name _ "</name>\r\n";
											expr "<version>" _ ./version _ "</version>\r\n";
											expr "<part-number>" _ ./part-number _ "</part-number>\r\n";
											expr "<serial-number>" _ ./serial-number _ "</serial-number>\r\n";
											expr "<description>" _ ./description _ "</description>\r\n";
											expr "<clei-code>" _ ./clei-code _ "</clei-code>\r\n";
											expr "<model-number>" _ ./model-number _ "</model-number>\r\n";
											for-each ( current()/chassis-sub-sub-module ) {
												expr "<chassis-sub-sub-module>\r\n";
													expr "<name>" _ ./name _ "</name>\r\n";
													expr "<part-number>" _ ./part-number _ "</part-number>\r\n";
													expr "<serial-number>" _ ./serial-number _ "</serial-number>\r\n";
													expr "<description>" _ ./description _ "</description>\r\n";
													for-each ( current()/chassis-sub-sub-sub-module ) {
														expr "<chassis-sub-sub-sub-module>\r\n";
															expr "<name>" _ ./name _ "</name>\r\n";
															expr "<part-number>" _ ./part-number _ "</part-number>\r\n";
															expr "<serial-number>" _ ./serial-number _ "</serial-number>\r\n";
															expr "<description>" _ ./description _ "</description>\r\n";
														expr "</chassis-sub-sub-sub-module>\r\n";
													}
												expr "</chassis-sub-sub-module>\r\n";
											}
										expr "</chassis-sub-module>\r\n";
									}
								expr "</chassis-module>\r\n";
							}
						expr "</chassis-inventory>\r\n";
					}
					var $plaintextVersion = {
						expr "Hostname: " _ $versionResults/host-name _ "\r\n";
						expr "Version: " _ $versionResults/product-model _ "\r\n";
						expr "Package Name, Version\r\n\r\n";
						for-each ( $versionResults/package-information ) {
							expr "\t" _ ./name _ "," _ ./comment _ "\r\n";
						}
					}
					
					/* write files and save results in var */
					
					var $versionSendResponse := {
						var $fileName = $myDevice _ "_version_" _ $outputFileTime _ "_bng_inventory";
						var  $write-rpc = <file-put> {
							<filename> $destinationPath _ $fileName;
							<permission> "644";
							<encoding> "ascii";
							<delete-if-exist>;
							<file-contents> $plaintextVersion;
						}
						var $results = jcs:execute( $connection, $write-rpc );
						if ( $results//self::xnm:error ) {
							<msg> "failure";
							<error> $results//self::xnm:error;
						} else {
							<msg> "success";
						}
					}
					
					var $hardwareSendResponse := {
						var $fileName = $myDevice _ "_hardware_" _ $outputFileTime _ "_bng_inventory";
						var  $write-rpc = <file-put> {
							<filename> $destinationPath _ $fileName;
							<permission> "644";
							<encoding> "ascii";
							<delete-if-exist>;
							<file-contents> $plaintextHardware;
						}
						var $results = jcs:execute( $connection, $write-rpc );
						if ( $results//self::xnm:error ) {
							<msg> "failure";
							<error> $results//self::xnm:error;
						} else {
							<msg> "success";
						}
					}
					
					var $hardwareXMLSendResponse := {
						var $fileName = $myDevice _ "_hw_xml_" _ $outputFileTime _ "_bng_inventory";
						var  $write-rpc = <file-put> {
							<filename> $destinationPath _ $fileName;
							<permission> "644";
							<encoding> "ascii";
							<delete-if-exist>;
							<file-contents> $xmlHardware;
						}
						var $results = jcs:execute( $connection, $write-rpc );
						if ( $results//self::xnm:error ) {
							<msg> "failure";
							<error> $results//self::xnm:error;
						} else {
							<msg> "success";
						}
					}
					
					<p> {
						expr "Destination path: "; <span class="italic"> $destinationPath;
					}
					<div> {
						var $htmlHardwareOutput = str:split( $plaintextHardware, "\r\n" );
						<p class="bold subheader">{
							<span class="left"> { expr "Output Hardware"; }
						}
						
						expr "The whole output of the command can be opened via the button.";
						
						<p class="hiddenButton" onclick="toggle(this, 1);">{
							expr "Show command response";
							<span id="hiddenButton_1_span" class="hiddenButtonSpan"> { expr ">"; }
						}
						<div id="hiddenDiv_1" class="hidden data" style="display: none;"> {
							for-each ( $htmlHardwareOutput ) {
								var $lpadding = {
									if ( contains ( ., "\t\t\t" ) ) {
										expr "lpadding30";
									} else if ( contains ( ., "\t\t" ) ) {
										expr "lpadding15";
									} else if ( contains ( ., "\t" ) ) {
										expr "lpadding5";
									} else {
										expr "";
									}
								}
								<span class="" _ $lpadding _ ""> .; <br>;
							}
						}
					}
					<div> {
						var $htmlVersionOutput = str:split( $plaintextVersion, "\r\n" );
						<p class="bold subheader">{
							<span class="left"> { expr "Output Version"; }
						}
						
						expr "The whole output of the command can be opened via the button.";
						
						<p class="hiddenButton" onclick="toggle(this, 2);">{
							expr "Show command response";
							<span id="hiddenButton_2_span" class="hiddenButtonSpan"> { expr ">"; }
						}
						<div id="hiddenDiv_2" class="hidden data" style="display: none;"> {
							for-each ( $htmlVersionOutput ) {
								var $lpadding = {
									if ( contains ( ., "\t\t\t" ) ) {
										expr "lpadding30";
									} else if ( contains ( ., "\t\t" ) ) {
										expr "lpadding15";
									} else if ( contains ( ., "\t" ) ) {
										expr "lpadding5";
									} else {
										expr "";
									}
								}
								<span class="" _ $lpadding _ ""> .; <br>;
							}
						}
					}
					
					var $syslogString = { 
						expr "Script Event - BNG Juniper Inventar vs" _ $version _ ". ";
						expr "Version Sending: " _ $versionSendResponse/msg _ ". ";
						expr "Hardware Sending: " _ $hardwareSendResponse/msg _ ". ";
						expr "Hardware XML Sending: " _ $hardwareXMLSendResponse/msg _ ". ";
					}
					
					<div> {
						<table class="twoColumnTable middle"> {
							<tr> {
								<th> { expr "Part"; }
								<th> { expr "Result"; }
							}
							<tr> {
								<td> { expr "Version:"; }
								<td> { 
									if ( $versionSendResponse/msg == "success" ) {
										<span class="infoError"> $versionSendResponse/msg; 
									} else {
										<span class="majorError" title="" _ $hardwareXMLSendResponse/error> $versionSendResponse/msg; 
									}
								}
							}
							<tr> {
								<td> { expr "Hardware:"; }
								<td> { 
									if ( $hardwareSendResponse/msg == "success" ) {
										<span class="infoError"> $hardwareSendResponse/msg; 
									} else {
										<span class="majorError" title="" _ $hardwareXMLSendResponse/error> $hardwareSendResponse/msg; 
									}
								}
							}
							<tr> {
								<td> { expr "Hardware XML:"; }
								<td> { 
									if ( $hardwareXMLSendResponse/msg == "success" ) {
										<span class="infoError"> $hardwareXMLSendResponse/msg; 
									} else {
										<span class="majorError" title="" _ $hardwareXMLSendResponse/error> $hardwareXMLSendResponse/msg; 
									}
								}
							}
						}
					}
					
					var $duration = date:seconds() - date:seconds( $localtime );
					<div class="wholeLine"> {
						<span class="left italic unselectable"> "vs. " _ $version;
						expr "Dauer der Ausf"; <xsl:text disable-output-escaping="yes"> "&uuml;"; expr "hrung: " _ $duration _ "s. ";
					}
				}
			}
		}
		
		set $unlock-results = jcs:execute( $connection, "unlock-database" );
		expr jcs:close( $connection );
		/*<output> "------------------------------------------------";
		<output> " ****  ****  **** End of script ****  ****  ****";
		<output> "------------------------------------------------";*/
		
	}
}

template checkErrors ( $resultsNodeSet, $msg = "", $connection ) {
	if( $resultsNodeSet//self::xnm:error ) {
		expr $msg; <br>;
		expr jcs:syslog("user.error", "Script Event - BNG Juniper Inventar: " _ $msg);
		for-each ($rpc//xnm:error) {
			expr .; <br>;
			expr jcs:syslog("user.error", .);
		}
	}
}

/*

	file archive compress source "/var/log/*" destination  file archive compress source /var/log/* destination conf-save@10.201.100.100:/home/conf-save/scriptsTests/BNG4_log.tgz

		<file-archive>
                <compress/>
                <destination>conf-save@10.201.100.100:/home/conf-save/scriptsTest/BNG4_log.tgz</destination>
                <source>"/var/log/*"</source>
        </file-archive>

*/