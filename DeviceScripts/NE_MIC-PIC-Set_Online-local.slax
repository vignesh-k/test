/*
 * Filename      : NE_MIC-PIC-Set_Online-local.slax
 * Author        : Andrew Sharp asharp@juniper.net
 * Platform      : Junos Space
 * Release       : 13.1P2
 * Version       : 1.0
 * SVN INFO      :
 *
 * $Rev: 36133 $
 * $Date: 2014-04-24 14:14:09 +0100 (Thu, 24 Apr 2014) $
 * $Author: asharp $
 * 
 * Description   : Take PIC online.
 *
 */

version 1.0;

/* Namespace declarations */
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns jspace = "http://jspace-utils/asharp@juniper.net";

/* Imports */
import "../import/junos.xsl";
import "lc-jspace-lib.slax";

/* Junos Space specific context, name and description */
/* @CONTEXT = "/device/chassis-inventory/chassis/chassis-module[starts-with(name,"FPC")]/chassis-sub-module[starts-with(name,"MIC")]/chassis-sub-sub-module[starts-with(name,"PIC")]" */
/* @NAME = "Bring PIC Online" */
/* @DESCRIPTION = "Take PIC online." */
/* @ISLOCAL = "true" */

/* Global variables */
var $scriptname = "NE_MIC-PIC-Set_Online-local.slax";
var $results;
var $regex;
var $result-regex;

var $arguments = {
	<argument> {
		<name> "CONTEXT";
		<description> "The context associated with this script.";
	}
}
param $CONTEXT;

match / {
	<op-script-results> {
		var $regex = "/device.*/chassis-inventory/chassis\\[name=\"(.*)\"\\]/chassis-module\\[name=\"(.* ([0-9]+))\"\\]/chassis-sub-module\\[name=.*\\]/chassis-sub-sub-module\\[name=\"(.* ([0-9]+))\"\\]";
		var $result-regex = jcs:regex( $regex , $CONTEXT );
		
		/* Request PIC online */
		var $command = {
			<command> "request chassis pic online fpc-slot " _ $result-regex[4] _ " pic-slot " _ $result-regex[6];
		}
		var $connection = jcs:open();
		/* abort if no connection to local mgd */
		if ($connection/..//xnm:error) {
			call rpc_failure($rpc = $connection/.., $message = "Error connecting on mgd on this RE");
			<xsl:message terminate="yes"> ;
		}
		var $results = jcs:execute( $connection , $command );
		if ($results/..//xnm:error) {
			call rpc_failure($rpc = $results/.., $message = "Error executing command request chassis pic offline fpc-slot");
			<xsl:message terminate="yes"> ;
		}

		<output> {
			<HTML> {
				<HEAD> {
					<title> "PIC online";
          copy-of jspace:html-style("3");
				}
				<BODY> {
					<p> {
						copy-of $results;
					}
				}
			}
		}
		if ( contains( $results , "already offline" ) ) {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_PIC_ONLINE: Script action failed to online the Physical Interface Card (PIC), as it is online already.");
		}
		else if ( contains( $results , "does not support this command" ) ) {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_PIC_ONLINE: Script action failed to online the Physical Interface Card (PIC), as it does not support this command.");
		}
		else {
			expr jcs:syslog("daemon.info", "SCRIPT_ACTION_PIC_ONLINE: Script action taken to online the Physical Interface Card (PIC).");
		}
		var $close-results = jcs:close( $connection );
		if ($close-results/..//xnm:error) {
			call rpc_failure($rpc = $close-results/.., $message = "Error closing connection.");
			<xsl:message terminate="yes"> ;
		}
	}
}

template rpc_failure($rpc, $message = "Following errors occurred while trying to gather data: ") {
  expr jcs:syslog("daemon.error", $message);
  for-each ($rpc//xnm:error) {
    expr jcs:syslog("daemon.error", message);
  }
}
